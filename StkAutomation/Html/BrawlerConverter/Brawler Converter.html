
<!-- saved from url=(0134)http://agi.vo.llnwd.net/v1/s/resources/code-snippets-samples/BrawlerConverter_STK.HTML?e=1484668465&h=1ff39d53402da005e5126b704ccddd19 -->
<html><head><meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
  <title>Brawler Converter</title>
  
  <meta name="description" content="This HTML page converts data from Brawler into an STK Object">

<script language="VBSCRIPT">
  <!--
    Dim uiApp, scen, root
	Dim ScenarioDirectory
	Const ForReading = 1
	Const ForWriting = 2

	
	Sub InitPage()
		'Initializes the HTML Page
    	Set uiApp = window.external
    	Set root = uiApp.Personality2
    	Set scen = root.CurrentScenario
		Set ret = root.ExecuteCommand("GetDirectory / Scenario")
		ScenarioDirectory = ret(0)
	End Sub
	
	
	Sub selectBrawlerFiles()
		dim fileOpenResult, file, colOptions, objOption, i
		Set fileOpenResult = uiApp.FileOpenDialogExt(true, "*.1", "Brawler Files (*.1)|*.1|", ".1")
		
		'Clear out List box of existing files
		For Each objOption in brawlerFileList.Options
			objOption.RemoveNode
		Next
		
		'Populate list box with file names
		Set colOptions = brawlerFileList.options
		For i = 0 to fileOpenResult.filename.count-1
			Set objOption = document.createelement("OPTION")
			colOptions.add(objOption)
			objOption.innerText = fileOpenResult.filename(i)
		Next
	End Sub
	
	
	Sub CreateSTKObject() 
		Dim brawlerObjectName, eFile, eFilePath, aFile, aFilePath
		Dim brawlerFile, numberOfLines, regEx, line, dataPoints
		Dim brawlerObject, externalEphemeris
		
		objType = objTypeList.Options(objTypeList.SelectedIndex).Text
		
		'Perform conversion for each file in list
		For Each file In brawlerFileList.Options
			
			'Set file path to list value
			brawlerFilePath = file.innerText
			
			Set fso = createObject("Scripting.FileSystemObject")
			brawlerObjectName = fso.GetBaseName(brawlerFilePath)
			
			'Create .e and .a files in scenario directory
			eFilePath = ScenarioDirectory & "\" & brawlerObjectName & ".e"
			Set eFile = fso.CreateTextFile(eFilePath, true)
			aFilePath = ScenarioDirectory & "\" & brawlerObjectName & ".a"
			Set aFile = fso.CreateTextFile(aFilePath, true)
			
			'Read in Brawler file and get number of points in file
			Set brawlerFile = fso.OpenTextFile(brawlerFilePath, 1)
			brawlerFile.ReadAll
			numberOfLines = brawlerFile.Line - 2
			brawlerFile.Close
			
			'Write .e Header
			eFile.WriteLine("stk.v.8.0")
			eFile.WriteLine("BEGIN Ephemeris")
			eFile.WriteLine("NumberofEphemerisPoints" & VBTab & numberOfLines)
			eFile.WriteLine("InterpolationMethod" & VBTab & VBTab & "Lagrange")
			eFile.WriteLine("CentralBody" & VBTab & VBTab & VBTab & VBTab & "Earth")
			eFile.WriteLine("CoordinateSystem" & VBTab & VBTab & "Custom BrawlerSystem Facility/BrawlerOrigin")
			eFile.WriteLine("DistanceUnit" & VBTab & VBTab & VBTab & "Feet")
			eFile.WriteLine()
			If objType = "Aircraft" Then
				eFile.WriteLine("EphemerisTimePosVelAcc" & vbnewline)
			ElseIf objType = "Missile" Then
				eFile.WriteLine("EphemerisTimePos" & vbnewline)
			End If
			
			'Write .a Header
			aFile.WriteLine("stk.v.8.0")
			aFile.WriteLine("BEGIN Attitude")
			aFile.WriteLine("NumberofAttitudePoints" & VBTab & numberOfLines)
			aFile.WriteLine("InterpolationMethod" & VBTab & VBTab & "Lagrange")
			aFile.WriteLine("CentralBody" & VBTab & VBTab & VBTab & VBTab & "Earth")
			aFile.WriteLine("CoordinateAxes" & VBTab & VBTab & VBTab & "Custom VVLH")
			aFile.WriteLine("Sequence 321")
			aFile.WriteLine()
			aFile.WriteLine("AttitudeTimeEulerAngles" & vbnewline)
			
			'Create search pattern for numerical data to avoid pulling header of Brawler file
			Set regEx = New RegExp
			With regEx
				.Pattern = "(^\d|-)"
				.Global = False
				.IgnoreCase = False
			End With
			
			'Open Brawler file and read each line then convert to .e or .a format
			Set brawlerFile = fso.OpenTextFile(brawlerFilePath, 1)
			Do Until brawlerFile.AtEndOfStream
				line = brawlerFile.ReadLine
				If regEx.Test(line) = True Then
					'Store individual points in an array
					dataPoints = Split(line, ",")
					
					If objType = "Aircraft" Then
						'First 10 data points in Brawler file (0-9 in array) fire are time, position, velocity, and acceleration points.
						'Writing only those in .e file.
						For i = 0 to 9
							eFile.Write(dataPoints(i))
						Next
						eFile.WriteLine()
					
						'Write .a file time stamp. YPR angles are elements 10, 11, 12 in array. Writing those after time stamp to .a file
						aFile.Write(dataPoints(0))
						For i = 10 to 12
							aFile.Write(dataPoints(i))
						Next
						aFile.WriteLine()
					ElseIf objType = "Missile" Then
						'Missile file time stamp is at index zero, position elements are 8, 9, 10. Z position is negative to follow Brawler Coordinate System
						eFile.Write(dataPoints(0))
						eFile.Write(dataPoints(8))
						eFile.Write(dataPoints(9))
						eFile.Write("     -" & Trim(dataPoints(10)))
						eFile.WriteLine()
						
						'Write .a file time stamp and YPR angles. Brawler only provides pitch so assuming zero for yaw and roll.
						aFile.Write(dataPoints(0))
						aFile.Write(dataPoints(12))
						aFile.Write("0" & VBTab)
						aFile.Write("0" & VBTab)
						aFile.WriteLine()
					End If
						
				End If
			Loop
			brawlerFile.Close
			
			'Write footers and close .e and .a files
			eFile.WriteLine("END Ephemeris")
			aFile.WriteLine("END Attitude")
			eFile.Close
			aFile.Close
			
			'Create STK Object
			If objType = "Aircraft" Then
				Set brawlerObject = scen.Children.New(1 , brawlerObjectName)
				
				'Set propagator to STK External and apply .e file
				brawlerObject.SetRouteType(6)
				Set externalEphemeris = brawlerObject.Route
				externalEphemeris.Filename = eFilePath
				externalEphemeris.Override = False
				externalEphemeris.Propagate()
				
				'Apply .a file using Connect
				root.ExecuteCommand("SetAttitude */Aircraft/" + brawlerObjectName + " File """ + aFilePath + """")
			ElseIf objType = "Missile" Then
				Set brawlerObject = scen.Children.New(13 , brawlerObjectName)
				root.ExecuteCommand("VO */Missile/" + brawlerObjectName + " Pass3D GroundLead None GroundTrail None")
				
				'Set propagator to STK External and apply .e file
				brawlerObject.SetTrajectoryType(6)
				Set externalEphemeris = brawlerObject.Trajectory
				externalEphemeris.Filename = eFilePath
				externalEphemeris.Override = False
				externalEphemeris.Propagate()
				
				'Apply .a file using Connect
				root.ExecuteCommand("SetAttitude */Missile/" + brawlerObjectName + " File """ + aFilePath + """")
			End If
		Next
	End Sub
	
	
	Sub CreateBrawlerCoordinateSystem()
		Dim brawlerOrigin, brawlerAxes, brawlerSystem
		
		'Create Facility object to serve as origin and do not show it
		Set brawlerOrigin = scen.Children.New(8, "BrawlerOrigin")
		root.UnitPreferences.SetCurrentUnit "Latitude", "deg"
		root.UnitPreferences.SetCurrentUnit "Longitude", "deg"
		brawlerOrigin.Position.AssignPlanetodetic brawlerSystemLat.value, brawlerSystemLong.value, 0.0
		root.ExecuteCommand("Graphics */Facility/BrawlerOrigin Label Show Off")
		root.ExecuteCommand("Graphics */Facility/BrawlerOrigin Marker None")
		root.ExecuteCommand("VO */Facility/BrawlerOrigin Model Show Off")
		root.ExecuteCommand("VO */Facility/BrawlerOrigin Marker Show Off")
		
		'Create Brawler Aligned and Constrained Axes with x-direction due East, y-direction due South, and z-direction "down" as completed by right-hand rule
		Set brawlerAxes = brawlerOrigin.Vgt.Axes.Factory.Create("BrawlerAxes", "", 7)
		brawlerAxes.AlignmentDirection.AssignXYZ 1, 0, 0
		brawlerAxes.AlignmentReferenceVector.SetVector(brawlerOrigin.Vgt.Vectors.Item("East"))
        brawlerAxes.ConstraintDirection.AssignXYZ 0, 1, 0
        brawlerAxes.ConstraintReferenceVector.SetVector(brawlerOrigin.Vgt.Vectors.item("South"))
		
		'Create Brawler Coordinate System for use with .e file. Origin at center of BrawlerOrigin
		Set brawlerSystem = brawlerOrigin.Vgt.Systems.Factory.Create("BrawlerSystem", "", 0)
		brawlerSystem.OriginPoint.SetPoint(brawlerOrigin.Vgt.Points.Item("Center"))
        brawlerSystem.ReferenceAxes.SetAxes(brawlerOrigin.Vgt.Axes.Item("BrawlerAxes"))	
	End Sub
	
	
	Sub Execute()
		dim brawlerOriginExists, brawlerAxesExists, brawlerSystemExists, objectType
		brawlerOriginExists = scen.Children.Contains(8, "BrawlerOrigin")
		'Check if BrawlerOrigin object exists, create it if necessary
		'Then check for axes/coordinate system and create if necessary
		If brawlerOriginExists = False Then
			CreateBrawlerCoordinateSystem()
		Else
			Set brawlerOrigin = root.GetObjectFromPath("Facility/BrawlerOrigin")
			brawlerAxesExists = brawlerOrigin.Vgt.Axes.Contains("BrawlerAxes")
			brawlerSystemExists = brawlerOrigin.Vgt.Systems.Contains("BrawlerSystem")
			
			If brawlerAxesExists = False Then
				scen.Children.Unload 8,"BrawlerOrigin"
				CreateBrawlerCoordinateSystem()
			ElseIf brawlerSystemExists = False Then
				scen.Children.Unload 8,"BrawlerOrigin"
				CreateBrawlerCoordinateSystem()
			End If
		End If
		
		CreateSTKObject()	
	End Sub
	
	
	Sub UnLoadPage()
        Set uiApp = Nothing
        Set root = Nothing
    End Sub
 
	
// -->
</script>




</head><body onload="InitPage()" onunload="UnLoadPage()" class="light" marginheight="10" marginwidth="10" leftmargin="10" topmargin="10" align="center">
	<b><u>
	Convert Brawler Data for Use in STK
	</u></b>

	<!-- Choose the Object Type-->
	<br><br>
	Object Type:
	<select id="objTypeList" name="objTypeList">
	<option value="ac">Aircraft</option>
	<option value="mi">Missile</option>
	</select>
	

	<!-- Choose the Brawler files-->
	<br><br>
	<input type="button" name="brawlerFileLocation" value="Select Brawler Files" onclick="selectBrawlerFiles()"><br><br>
	<select id="brawlerFileList" name="brawlerFileList" size="10" width="150">
	</select>
	
	
	<!-- Set lat/lon of origin-->
	<br><br>
	Brawler Scenario Origin: <br>
	&nbsp;&nbsp;&nbsp;Latitude:&nbsp; <input type="text" name="brawlerSystemLat" size="10" value="0.0"> <br>
	Longitude:&nbsp; <input type="text" name="brawlerSystemLong" size="10" value="0.0"> <br>
	
	<br><br>
	<input type="button" name="convertToSTK" value="Convert to STK Objects" onclick="Execute()">
	

</body></html>